plugins {
    id 'com.android.application'
}

android {

    // SDK 30 is incompatible for gomobile net.InterfaceAddrs()
    // https://github.com/golang/go/issues/40569
    compileSdkVersion 28
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "network.datahop.datahopdemo"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

repositories {
    maven {
        name = "GitHubPackages"
        /*  Configure path to the library hosted on GitHub Package Registry
     *  Replace UserID with package owner userID and REPOSITORY with the repository name
     *  e.g. "https://maven.pkg.github.com/enefce/AndroidLibraryForGitHubPackagesDemo"
     */
        url = uri("https://maven.pkg.github.com/datahop/ipfs-lite")

        credentials {
            username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
            password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.gms:play-services-location:17.1.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation "com.google.protobuf:protobuf-java:3.15.8"

    implementation 'network.datahop.libs:datahop:v0.0.11'
    implementation 'network.datahop.libs:blediscovery:0.0.5'
    implementation 'network.datahop.libs:wifidirect:0.0.2'
}